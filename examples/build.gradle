
dependencies {
    compile project(':main:boofcv-ip')
    compile project(':main:boofcv-io')
    compile project(':main:boofcv-feature')
    compile project(':main:boofcv-geo')
    compile project(':main:boofcv-calibration')
    compile project(':main:boofcv-sfm')
    compile project(':main:boofcv-learning')
    compile project(':main:boofcv-recognition')
    compile project(':integration:boofcv-swing')
    compile project(':integration:boofcv-ffmpeg')
    compile project(':integration:boofcv-WebcamCapture')

    compile 'org.reflections:reflections:0.9.10'
}

// Only add javafx as a dependency if it exists. The code will run just fine without it,
// but some visualizations look better
try {
    project.project(':integration:boofcv-javafx')
    dependencies {
        compile project(':integration:boofcv-javafx')
    }
} catch( UnknownProjectException ignore ) {}

idea {
    module {
        name = "BoofCV Examples"
    }
}

// Create a jar which contains all the applications
task examples(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'BoofCV Examples Jar',
                'Implementation-Version': version,
                'Main-Class': 'boofcv.examples.ExampleLauncherApp'
    }

    outputs.upToDateWhen { false }

    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar

    doLast {
        archivePath.renameTo(file('examples.jar'))
    }
}